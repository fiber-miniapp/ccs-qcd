subroutine mult_eo_tzyx(yo,yde,ue,uo,ieo,fclinve)
!***********************************************************************
!$Id: mult_eo_tzyx.h90,v 1.1 2009/12/02 10:24:23 ishikawa Exp $
!
! Multiply hopping matrix : yde = Fee^-1 Meo yo
!
!      yo : odd/even site quark field (ieo=0/1) (input),
!           ghost sites should be filled before calling this routine.
!     yde : even/odd site quark field (ieo=0/1) (output)
!      ue : even/odd site gauge link (ieo=0/1)
!      uo : odd/even site gauge link (ieo=0/1)
!     ieo : operation index
!              ieo=0 for even site
!              ieo=1 for odd  site
! fclinve : even/odd site inverse clover term (ieo=0/1) (chiral rep. linear form)
!
!***********************************************************************
  implicit none
  complex(8), intent(in):: ue(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM)
  complex(8), intent(in):: uo(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM)
  complex(8), intent(in)   ::  yo(COL,SPIN,0:NTH,0:NZ1,0:NY1,0:NX1)
  complex(8), intent(inout):: yde(COL,SPIN,0:NTH,0:NZ1,0:NY1,0:NX1)
  integer, intent(in) :: ieo
  complex(8), intent(in) :: fclinve(CLSPH,0:NTH,NZ,NY,NX,2)
  complex(8):: a1,a2,a3,a4,a5,a6
  complex(8):: b1,b2,b3,b4,b5,b6
  complex(8):: p1,p2,p3,p4,p5,p6
  complex(8):: m1,m2,m3,m4,m5,m6
  complex(8) :: yt(COL,SPIN)
  complex(8) :: gy11,gy21,gy31,gy12,gy22,gy32
  complex(8) :: hp11,hp21,hp31,hp12,hp22,hp32
  integer :: ix,iy,iz,itb,itbup,itbdn,ieoxyz,ic

!$OMP PARALLEL  &
!$OMP& PRIVATE(yt,ix,iy,iz,ieoxyz,itb,itbup,itbdn,ic,  &
!$OMP&         a1,a2,a3,a4,a5,a6, &
!$OMP&         b1,b2,b3,b4,b5,b6, &
!$OMP&         p1,p2,p3,p4,p5,p6, &
!$OMP&         m1,m2,m3,m4,m5,m6, &
!$OMP&         gy11,gy21,gy31,gy12,gy22,gy32,  &
!$OMP&         hp11,hp21,hp31,hp12,hp22,hp32)
!$OMP DO COLLAPSE(2)
  do ix=1,NX
  do iy=1,NY         
  do iz=1,NZ
    ieoxyz = mod(ipeo+ix+iy+iz+ieo,2)
    itbup =   +ieoxyz 
    itbdn = -(1-ieoxyz)
    do itb=1-ieoxyz,NTH-ieoxyz

!**************
! T-direction
!**************
!**************
! forward
!**************
    ic=1
      yt(ic,3) =    &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*    &
 &                    yo( 1,3,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*    &
 &                    yo( 2,3,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*    &
 &                    yo( 3,3,itb+itbup,iz,iy,ix) )
      yt(ic,4) =     &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*    &
 &                    yo( 1,4,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*    &
 &                    yo( 2,4,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*    &
 &                    yo( 3,4,itb+itbup,iz,iy,ix) )
    ic=2
      yt(ic,3) =     &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*    &
 &                    yo( 1,3,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*    &
 &                    yo( 2,3,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*    &
 &                    yo( 3,3,itb+itbup,iz,iy,ix) )
      yt(ic,4) =     &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*    &
 &                    yo( 1,4,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*    &
 &                    yo( 2,4,itb+itbup,iz,iy,ix)    &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*    &
 &                    yo( 3,4,itb+itbup,iz,iy,ix) )
    ic=3
      yt(ic,3) =      &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*     &
 &                    yo( 1,3,itb+itbup,iz,iy,ix)     &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*     &
 &                    yo( 2,3,itb+itbup,iz,iy,ix)     &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*     &
 &                    yo( 3,3,itb+itbup,iz,iy,ix) )
      yt(ic,4) =      &
 &    + 2.0d0*(       ue(ic,1,itb,      iz,iy,ix,4)*     &
 &                    yo( 1,4,itb+itbup,iz,iy,ix)     &
 &            +       ue(ic,2,itb,      iz,iy,ix,4)*     &
 &                    yo( 2,4,itb+itbup,iz,iy,ix)     &
 &            +       ue(ic,3,itb,      iz,iy,ix,4)*     &
 &                    yo( 3,4,itb+itbup,iz,iy,ix) )
!**************
! bakward
!**************
    ic=1
      yt(ic,1) =     &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 1,itb+itbdn,iz,iy,ix)   )
      yt(ic,2) =      &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 2,itb+itbdn,iz,iy,ix)   )
    ic=2
      yt(ic,1) =      &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 1,itb+itbdn,iz,iy,ix)   )
      yt(ic,2) =      &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 2,itb+itbdn,iz,iy,ix)   )
    ic=3
      yt(ic,1) =      &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 1,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 1,itb+itbdn,iz,iy,ix)   )
      yt(ic,2) =      &
 &    + 2.0d0*( conjg(uo(1,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(1, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(2,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(2, 2,itb+itbdn,iz,iy,ix)     &
 &            + conjg(uo(3,ic,itb+itbdn,iz,iy,ix,4))*     &
 &                    yo(3, 2,itb+itbdn,iz,iy,ix)   )

!**************
! Z - direction
!**************
!**************
! forward
!**************
      gy11 = dcmplx(     &
 &              dble(yo(1,1,itb,iz+1,iy,ix))      &
 &           - aimag(yo(1,3,itb,iz+1,iy,ix)),     &
 &             aimag(yo(1,1,itb,iz+1,iy,ix))     &
 &           +  dble(yo(1,3,itb,iz+1,iy,ix)) )
      gy21 = dcmplx(     &
 &              dble(yo(2,1,itb,iz+1,iy,ix))      &
 &           - aimag(yo(2,3,itb,iz+1,iy,ix)),     &
 &             aimag(yo(2,1,itb,iz+1,iy,ix))     &
 &           +  dble(yo(2,3,itb,iz+1,iy,ix)) )
      gy31 = dcmplx(     &
 &              dble(yo(3,1,itb,iz+1,iy,ix))      &
 &           - aimag(yo(3,3,itb,iz+1,iy,ix)),     &
 &             aimag(yo(3,1,itb,iz+1,iy,ix))     &
 &           +  dble(yo(3,3,itb,iz+1,iy,ix)) )

      gy12 = dcmplx(     &
 &              dble(yo(1,2,itb,iz+1,iy,ix))      &
 &           + aimag(yo(1,4,itb,iz+1,iy,ix)),     &
 &             aimag(yo(1,2,itb,iz+1,iy,ix))     &
 &           -  dble(yo(1,4,itb,iz+1,iy,ix)) )
      gy22 = dcmplx(     &
 &              dble(yo(2,2,itb,iz+1,iy,ix))      &
 &           + aimag(yo(2,4,itb,iz+1,iy,ix)),     &
 &             aimag(yo(2,2,itb,iz+1,iy,ix))     &
 &           -  dble(yo(2,4,itb,iz+1,iy,ix)) )
      gy32 = dcmplx(     &
 &              dble(yo(3,2,itb,iz+1,iy,ix))      &
 &           + aimag(yo(3,4,itb,iz+1,iy,ix)),     &
 &             aimag(yo(3,2,itb,iz+1,iy,ix))     &
 &           -  dble(yo(3,4,itb,iz+1,iy,ix)) )

      hp11 =     &
 &           ue(1,1,itb,iz,iy,ix,3) * gy11 +     &
 &           ue(1,2,itb,iz,iy,ix,3) * gy21 +     &
 &           ue(1,3,itb,iz,iy,ix,3) * gy31
      hp21 =     &
 &           ue(2,1,itb,iz,iy,ix,3) * gy11 +     &
 &           ue(2,2,itb,iz,iy,ix,3) * gy21 +     &
 &           ue(2,3,itb,iz,iy,ix,3) * gy31
      hp31 =     &
 &           ue(3,1,itb,iz,iy,ix,3) * gy11 +     &
 &           ue(3,2,itb,iz,iy,ix,3) * gy21 +     &
 &           ue(3,3,itb,iz,iy,ix,3) * gy31

      hp12 =     &
 &           ue(1,1,itb,iz,iy,ix,3) * gy12 +     &
 &           ue(1,2,itb,iz,iy,ix,3) * gy22 +     &
 &           ue(1,3,itb,iz,iy,ix,3) * gy32
      hp22 =     &
 &           ue(2,1,itb,iz,iy,ix,3) * gy12 +     &
 &           ue(2,2,itb,iz,iy,ix,3) * gy22 +     &
 &           ue(2,3,itb,iz,iy,ix,3) * gy32
      hp32 =     &
 &           ue(3,1,itb,iz,iy,ix,3) * gy12 +     &
 &           ue(3,2,itb,iz,iy,ix,3) * gy22 +     &
 &           ue(3,3,itb,iz,iy,ix,3) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) + dcmplx( aimag(hp11), -dble(hp11) )
      yt(2,3) = yt(2,3) + dcmplx( aimag(hp21), -dble(hp21) )
      yt(3,3) = yt(3,3) + dcmplx( aimag(hp31), -dble(hp31) )

      yt(1,4) = yt(1,4) + dcmplx(-aimag(hp12),  dble(hp12) )
      yt(2,4) = yt(2,4) + dcmplx(-aimag(hp22),  dble(hp22) )
      yt(3,4) = yt(3,4) + dcmplx(-aimag(hp32),  dble(hp32) )

!**************
! backward
!**************
      gy11 = dcmplx(     &
 &              dble(yo(1,1,itb,iz-1,iy,ix))      &
 &           + aimag(yo(1,3,itb,iz-1,iy,ix)),     &
 &             aimag(yo(1,1,itb,iz-1,iy,ix))     &
 &           -  dble(yo(1,3,itb,iz-1,iy,ix)) )
      gy21 = dcmplx(     &
 &              dble(yo(2,1,itb,iz-1,iy,ix))      &
 &           + aimag(yo(2,3,itb,iz-1,iy,ix)),     &
 &             aimag(yo(2,1,itb,iz-1,iy,ix))     &
 &           -  dble(yo(2,3,itb,iz-1,iy,ix)) )
      gy31 = dcmplx(     &
 &              dble(yo(3,1,itb,iz-1,iy,ix))      &
 &           + aimag(yo(3,3,itb,iz-1,iy,ix)),     &
 &             aimag(yo(3,1,itb,iz-1,iy,ix))     &
 &           -  dble(yo(3,3,itb,iz-1,iy,ix)) )

      gy12 = dcmplx(     &
 &              dble(yo(1,2,itb,iz-1,iy,ix))      &
 &           - aimag(yo(1,4,itb,iz-1,iy,ix)),     &
 &             aimag(yo(1,2,itb,iz-1,iy,ix))     &
 &           +  dble(yo(1,4,itb,iz-1,iy,ix)) )
      gy22 = dcmplx(     &
 &              dble(yo(2,2,itb,iz-1,iy,ix))      &
 &           - aimag(yo(2,4,itb,iz-1,iy,ix)),     &
 &             aimag(yo(2,2,itb,iz-1,iy,ix))     &
 &           +  dble(yo(2,4,itb,iz-1,iy,ix)) )
      gy32 = dcmplx(     &
 &              dble(yo(3,2,itb,iz-1,iy,ix))      &
 &           - aimag(yo(3,4,itb,iz-1,iy,ix)),     &
 &             aimag(yo(3,2,itb,iz-1,iy,ix))     &
 &           +  dble(yo(3,4,itb,iz-1,iy,ix)) )

      hp11 =     &
 &           conjg(uo(1,1,itb,iz-1,iy,ix,3)) * gy11 +     &
 &           conjg(uo(2,1,itb,iz-1,iy,ix,3)) * gy21 +     &
 &           conjg(uo(3,1,itb,iz-1,iy,ix,3)) * gy31
      hp21 =     &
 &           conjg(uo(1,2,itb,iz-1,iy,ix,3)) * gy11 +     &
 &           conjg(uo(2,2,itb,iz-1,iy,ix,3)) * gy21 +     &
 &           conjg(uo(3,2,itb,iz-1,iy,ix,3)) * gy31
      hp31 =     &
 &           conjg(uo(1,3,itb,iz-1,iy,ix,3)) * gy11 +     &
 &           conjg(uo(2,3,itb,iz-1,iy,ix,3)) * gy21 +     &
 &           conjg(uo(3,3,itb,iz-1,iy,ix,3)) * gy31

      hp12 =     &
 &           conjg(uo(1,1,itb,iz-1,iy,ix,3)) * gy12 +     &
 &           conjg(uo(2,1,itb,iz-1,iy,ix,3)) * gy22 +     &
 &           conjg(uo(3,1,itb,iz-1,iy,ix,3)) * gy32
      hp22 =     &
 &           conjg(uo(1,2,itb,iz-1,iy,ix,3)) * gy12 +     &
 &           conjg(uo(2,2,itb,iz-1,iy,ix,3)) * gy22 +     &
 &           conjg(uo(3,2,itb,iz-1,iy,ix,3)) * gy32
      hp32 =     &
 &           conjg(uo(1,3,itb,iz-1,iy,ix,3)) * gy12 +     &
 &           conjg(uo(2,3,itb,iz-1,iy,ix,3)) * gy22 +     &
 &           conjg(uo(3,3,itb,iz-1,iy,ix,3)) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) + dcmplx(-aimag(hp11),  dble(hp11) )
      yt(2,3) = yt(2,3) + dcmplx(-aimag(hp21),  dble(hp21) )
      yt(3,3) = yt(3,3) + dcmplx(-aimag(hp31),  dble(hp31) )

      yt(1,4) = yt(1,4) + dcmplx( aimag(hp12), -dble(hp12) )
      yt(2,4) = yt(2,4) + dcmplx( aimag(hp22), -dble(hp22) )
      yt(3,4) = yt(3,4) + dcmplx( aimag(hp32), -dble(hp32) )

!**************
! Y-direction
!**************
!**************
! forward
!**************
      gy11 = yo(1,1,itb,iz,iy+1,ix)     &
 &          +yo(1,4,itb,iz,iy+1,ix)
      gy21 = yo(2,1,itb,iz,iy+1,ix)     &
 &          +yo(2,4,itb,iz,iy+1,ix)
      gy31 = yo(3,1,itb,iz,iy+1,ix)     &
 &          +yo(3,4,itb,iz,iy+1,ix)

      gy12 = yo(1,2,itb,iz,iy+1,ix)     &
 &          -yo(1,3,itb,iz,iy+1,ix)
      gy22 = yo(2,2,itb,iz,iy+1,ix)     &
 &          -yo(2,3,itb,iz,iy+1,ix)
      gy32 = yo(3,2,itb,iz,iy+1,ix)     &
 &          -yo(3,3,itb,iz,iy+1,ix)

      hp11 =     &
 &           ue(1,1,itb,iz,iy,ix,2) * gy11 +     &
 &           ue(1,2,itb,iz,iy,ix,2) * gy21 +     &
 &           ue(1,3,itb,iz,iy,ix,2) * gy31
      hp21 =     &
 &           ue(2,1,itb,iz,iy,ix,2) * gy11 +     &
 &           ue(2,2,itb,iz,iy,ix,2) * gy21 +     &
 &           ue(2,3,itb,iz,iy,ix,2) * gy31
      hp31 =     &
 &           ue(3,1,itb,iz,iy,ix,2) * gy11 +     &
 &           ue(3,2,itb,iz,iy,ix,2) * gy21 +     &
 &           ue(3,3,itb,iz,iy,ix,2) * gy31
      hp12 =     &
 &           ue(1,1,itb,iz,iy,ix,2) * gy12 +     &
 &           ue(1,2,itb,iz,iy,ix,2) * gy22 +     &
 &           ue(1,3,itb,iz,iy,ix,2) * gy32
      hp22 =     &
 &           ue(2,1,itb,iz,iy,ix,2) * gy12 +     &
 &           ue(2,2,itb,iz,iy,ix,2) * gy22 +     &
 &           ue(2,3,itb,iz,iy,ix,2) * gy32
      hp32 =     &
 &           ue(3,1,itb,iz,iy,ix,2) * gy12 +     &
 &           ue(3,2,itb,iz,iy,ix,2) * gy22 +     &
 &           ue(3,3,itb,iz,iy,ix,2) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) - hp12
      yt(2,3) = yt(2,3) - hp22
      yt(3,3) = yt(3,3) - hp32

      yt(1,4) = yt(1,4) + hp11
      yt(2,4) = yt(2,4) + hp21
      yt(3,4) = yt(3,4) + hp31

!**************
! backward
!**************
      gy11 = yo(1,1,itb,iz,iy-1,ix)     &
 &          -yo(1,4,itb,iz,iy-1,ix)
      gy21 = yo(2,1,itb,iz,iy-1,ix)     &
 &          -yo(2,4,itb,iz,iy-1,ix)
      gy31 = yo(3,1,itb,iz,iy-1,ix)     &
 &          -yo(3,4,itb,iz,iy-1,ix)
      gy12 = yo(1,2,itb,iz,iy-1,ix)     &
 &          +yo(1,3,itb,iz,iy-1,ix)
      gy22 = yo(2,2,itb,iz,iy-1,ix)     &
 &          +yo(2,3,itb,iz,iy-1,ix)
      gy32 = yo(3,2,itb,iz,iy-1,ix)     &
 &          +yo(3,3,itb,iz,iy-1,ix)

      hp11 =     &
 &           conjg(uo(1,1,itb,iz,iy-1,ix,2)) * gy11 +     &
 &           conjg(uo(2,1,itb,iz,iy-1,ix,2)) * gy21 +     &
 &           conjg(uo(3,1,itb,iz,iy-1,ix,2)) * gy31
      hp21 =     &
 &           conjg(uo(1,2,itb,iz,iy-1,ix,2)) * gy11 +     &
 &           conjg(uo(2,2,itb,iz,iy-1,ix,2)) * gy21 +     &
 &           conjg(uo(3,2,itb,iz,iy-1,ix,2)) * gy31
      hp31 =     &
 &           conjg(uo(1,3,itb,iz,iy-1,ix,2)) * gy11 +     &
 &           conjg(uo(2,3,itb,iz,iy-1,ix,2)) * gy21 +     &
 &           conjg(uo(3,3,itb,iz,iy-1,ix,2)) * gy31
      hp12 =     &
 &           conjg(uo(1,1,itb,iz,iy-1,ix,2)) * gy12 +     &
 &           conjg(uo(2,1,itb,iz,iy-1,ix,2)) * gy22 +     &
 &           conjg(uo(3,1,itb,iz,iy-1,ix,2)) * gy32
      hp22 =     &
 &           conjg(uo(1,2,itb,iz,iy-1,ix,2)) * gy12 +     &
 &           conjg(uo(2,2,itb,iz,iy-1,ix,2)) * gy22 +     &
 &           conjg(uo(3,2,itb,iz,iy-1,ix,2)) * gy32
      hp32 =     &
 &           conjg(uo(1,3,itb,iz,iy-1,ix,2)) * gy12 +     &
 &           conjg(uo(2,3,itb,iz,iy-1,ix,2)) * gy22 +     &
 &           conjg(uo(3,3,itb,iz,iy-1,ix,2)) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) + hp12
      yt(2,3) = yt(2,3) + hp22
      yt(3,3) = yt(3,3) + hp32

      yt(1,4) = yt(1,4) - hp11
      yt(2,4) = yt(2,4) - hp21
      yt(3,4) = yt(3,4) - hp31

!**************
! X-direction
!**************
!**************
! forward
!**************
      gy11 = dcmplx(     &
 &              dble(yo(1,1,itb,iz,iy,ix+1))      &
 &           - aimag(yo(1,4,itb,iz,iy,ix+1)),     &
 &             aimag(yo(1,1,itb,iz,iy,ix+1))     &
 &           +  dble(yo(1,4,itb,iz,iy,ix+1)) )
      gy21 = dcmplx(     &
 &              dble(yo(2,1,itb,iz,iy,ix+1))      &
 &           - aimag(yo(2,4,itb,iz,iy,ix+1)),     &
 &             aimag(yo(2,1,itb,iz,iy,ix+1))     &
 &           +  dble(yo(2,4,itb,iz,iy,ix+1)) )
      gy31 = dcmplx(     &
 &              dble(yo(3,1,itb,iz,iy,ix+1))      &
 &           - aimag(yo(3,4,itb,iz,iy,ix+1)),     &
 &             aimag(yo(3,1,itb,iz,iy,ix+1))     &
 &           +  dble(yo(3,4,itb,iz,iy,ix+1)) )

      gy12 = dcmplx(     &
 &              dble(yo(1,2,itb,iz,iy,ix+1))      &
 &           - aimag(yo(1,3,itb,iz,iy,ix+1)),     &
 &             aimag(yo(1,2,itb,iz,iy,ix+1))     &
 &           +  dble(yo(1,3,itb,iz,iy,ix+1)) )
      gy22 = dcmplx(     &
 &              dble(yo(2,2,itb,iz,iy,ix+1))      &
 &           - aimag(yo(2,3,itb,iz,iy,ix+1)),     &
 &             aimag(yo(2,2,itb,iz,iy,ix+1))     &
 &           +  dble(yo(2,3,itb,iz,iy,ix+1)) )
      gy32 = dcmplx(     &
 &              dble(yo(3,2,itb,iz,iy,ix+1))      &
 &           - aimag(yo(3,3,itb,iz,iy,ix+1)),     &
 &             aimag(yo(3,2,itb,iz,iy,ix+1))     &
 &           +  dble(yo(3,3,itb,iz,iy,ix+1)) )

      hp11 =     &
 &           ue(1,1,itb,iz,iy,ix,1) * gy11 +     &
 &           ue(1,2,itb,iz,iy,ix,1) * gy21 +     &
 &           ue(1,3,itb,iz,iy,ix,1) * gy31
      hp21 =     &
 &           ue(2,1,itb,iz,iy,ix,1) * gy11 +     &
 &           ue(2,2,itb,iz,iy,ix,1) * gy21 +     &
 &           ue(2,3,itb,iz,iy,ix,1) * gy31
      hp31 =     &
 &           ue(3,1,itb,iz,iy,ix,1) * gy11 +     &
 &           ue(3,2,itb,iz,iy,ix,1) * gy21 +     &
 &           ue(3,3,itb,iz,iy,ix,1) * gy31
      hp12 =     &
 &           ue(1,1,itb,iz,iy,ix,1) * gy12 +     &
 &           ue(1,2,itb,iz,iy,ix,1) * gy22 +     &
 &           ue(1,3,itb,iz,iy,ix,1) * gy32
      hp22 =     &
 &           ue(2,1,itb,iz,iy,ix,1) * gy12 +     &
 &           ue(2,2,itb,iz,iy,ix,1) * gy22 +     &
 &           ue(2,3,itb,iz,iy,ix,1) * gy32
      hp32 =     &
 &           ue(3,1,itb,iz,iy,ix,1) * gy12 +     &
 &           ue(3,2,itb,iz,iy,ix,1) * gy22 +     &
 &           ue(3,3,itb,iz,iy,ix,1) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) + dcmplx( aimag(hp12), -dble(hp12) )
      yt(2,3) = yt(2,3) + dcmplx( aimag(hp22), -dble(hp22) )
      yt(3,3) = yt(3,3) + dcmplx( aimag(hp32), -dble(hp32) )

      yt(1,4) = yt(1,4) + dcmplx( aimag(hp11), -dble(hp11) )
      yt(2,4) = yt(2,4) + dcmplx( aimag(hp21), -dble(hp21) )
      yt(3,4) = yt(3,4) + dcmplx( aimag(hp31), -dble(hp31) )

!**************
! backward
!**************
      gy11 = dcmplx(     &
 &              dble(yo(1,1,itb,iz,iy,ix-1))      &
 &           + aimag(yo(1,4,itb,iz,iy,ix-1)),     &
 &             aimag(yo(1,1,itb,iz,iy,ix-1))     &
 &           -  dble(yo(1,4,itb,iz,iy,ix-1)) )
      gy21 = dcmplx(     &
 &              dble(yo(2,1,itb,iz,iy,ix-1))      &
 &           + aimag(yo(2,4,itb,iz,iy,ix-1)),     &
 &             aimag(yo(2,1,itb,iz,iy,ix-1))     &
 &           -  dble(yo(2,4,itb,iz,iy,ix-1)) )
      gy31 = dcmplx(     &
 &              dble(yo(3,1,itb,iz,iy,ix-1))      &
 &           + aimag(yo(3,4,itb,iz,iy,ix-1)),     &
 &             aimag(yo(3,1,itb,iz,iy,ix-1))     &
 &           -  dble(yo(3,4,itb,iz,iy,ix-1)) )
      gy12 = dcmplx(     &
 &              dble(yo(1,2,itb,iz,iy,ix-1))      &
 &           + aimag(yo(1,3,itb,iz,iy,ix-1)),     &
 &             aimag(yo(1,2,itb,iz,iy,ix-1))     &
 &           -  dble(yo(1,3,itb,iz,iy,ix-1)) )
      gy22 = dcmplx(     &
 &              dble(yo(2,2,itb,iz,iy,ix-1))      &
 &           + aimag(yo(2,3,itb,iz,iy,ix-1)),     &
 &             aimag(yo(2,2,itb,iz,iy,ix-1))     &
 &           -  dble(yo(2,3,itb,iz,iy,ix-1)) )
      gy32 = dcmplx(     &
 &              dble(yo(3,2,itb,iz,iy,ix-1))      &
 &           + aimag(yo(3,3,itb,iz,iy,ix-1)),     &
 &             aimag(yo(3,2,itb,iz,iy,ix-1))     &
 &           -  dble(yo(3,3,itb,iz,iy,ix-1)) )

      hp11 =     &
 &           conjg(uo(1,1,itb,iz,iy,ix-1,1)) * gy11 +     &
 &           conjg(uo(2,1,itb,iz,iy,ix-1,1)) * gy21 +     &
 &           conjg(uo(3,1,itb,iz,iy,ix-1,1)) * gy31
      hp21 =     &
 &           conjg(uo(1,2,itb,iz,iy,ix-1,1)) * gy11 +     &
 &           conjg(uo(2,2,itb,iz,iy,ix-1,1)) * gy21 +     &
 &           conjg(uo(3,2,itb,iz,iy,ix-1,1)) * gy31
      hp31 =     &
 &           conjg(uo(1,3,itb,iz,iy,ix-1,1)) * gy11 +     &
 &           conjg(uo(2,3,itb,iz,iy,ix-1,1)) * gy21 +     &
 &           conjg(uo(3,3,itb,iz,iy,ix-1,1)) * gy31
      hp12 =     &
 &           conjg(uo(1,1,itb,iz,iy,ix-1,1)) * gy12 +     &
 &           conjg(uo(2,1,itb,iz,iy,ix-1,1)) * gy22 +     &
 &           conjg(uo(3,1,itb,iz,iy,ix-1,1)) * gy32
      hp22 =     &
 &           conjg(uo(1,2,itb,iz,iy,ix-1,1)) * gy12 +     &
 &           conjg(uo(2,2,itb,iz,iy,ix-1,1)) * gy22 +     &
 &           conjg(uo(3,2,itb,iz,iy,ix-1,1)) * gy32
      hp32 =     &
 &           conjg(uo(1,3,itb,iz,iy,ix-1,1)) * gy12 +     &
 &           conjg(uo(2,3,itb,iz,iy,ix-1,1)) * gy22 +     &
 &           conjg(uo(3,3,itb,iz,iy,ix-1,1)) * gy32

      yt(1,1) = yt(1,1) + hp11
      yt(2,1) = yt(2,1) + hp21
      yt(3,1) = yt(3,1) + hp31

      yt(1,2) = yt(1,2) + hp12
      yt(2,2) = yt(2,2) + hp22
      yt(3,2) = yt(3,2) + hp32

      yt(1,3) = yt(1,3) + dcmplx(-aimag(hp12), dble(hp12) )
      yt(2,3) = yt(2,3) + dcmplx(-aimag(hp22), dble(hp22) )
      yt(3,3) = yt(3,3) + dcmplx(-aimag(hp32), dble(hp32) )

      yt(1,4) = yt(1,4) + dcmplx(-aimag(hp11), dble(hp11) )
      yt(2,4) = yt(2,4) + dcmplx(-aimag(hp21), dble(hp21) )
      yt(3,4) = yt(3,4) + dcmplx(-aimag(hp31), dble(hp31) )

!**********************************************************
! Calc:
!        
!  yde = F^-1ee ye
!      
!**********************************************************
      p1=yt(1,1)+yt(1,3)
      p2=yt(2,1)+yt(2,3)
      p3=yt(3,1)+yt(3,3)
      p4=yt(1,2)+yt(1,4)
      p5=yt(2,2)+yt(2,4)
      p6=yt(3,2)+yt(3,4)
      m1=yt(1,1)-yt(1,3)
      m2=yt(2,1)-yt(2,3)
      m3=yt(3,1)-yt(3,3)
      m4=yt(1,2)-yt(1,4)
      m5=yt(2,2)-yt(2,4)
      m6=yt(3,2)-yt(3,4)

      a1 =       fclinve( 1,itb,iz,iy,ix,2) * p1  &
 &              +fclinve( 2,itb,iz,iy,ix,2) * p2  &
 &              +fclinve( 3,itb,iz,iy,ix,2) * p3  &
 &              +fclinve( 4,itb,iz,iy,ix,2) * p4  &
 &              +fclinve( 5,itb,iz,iy,ix,2) * p5  &
 &              +fclinve( 6,itb,iz,iy,ix,2) * p6
      a2 =dconjg(fclinve( 2,itb,iz,iy,ix,2))* p1  &
 &              +fclinve( 7,itb,iz,iy,ix,2) * p2  &
 &              +fclinve( 8,itb,iz,iy,ix,2) * p3  &
 &              +fclinve( 9,itb,iz,iy,ix,2) * p4  &
 &              +fclinve(10,itb,iz,iy,ix,2) * p5  &
 &              +fclinve(11,itb,iz,iy,ix,2) * p6
      a3 =dconjg(fclinve( 3,itb,iz,iy,ix,2))* p1  &
 &       +dconjg(fclinve( 8,itb,iz,iy,ix,2))* p2  &
 &              +fclinve(12,itb,iz,iy,ix,2) * p3  &
 &              +fclinve(13,itb,iz,iy,ix,2) * p4  &
 &              +fclinve(14,itb,iz,iy,ix,2) * p5  &
 &              +fclinve(15,itb,iz,iy,ix,2) * p6
      a4 =dconjg(fclinve( 4,itb,iz,iy,ix,2))* p1  &
 &       +dconjg(fclinve( 9,itb,iz,iy,ix,2))* p2  &
 &       +dconjg(fclinve(13,itb,iz,iy,ix,2))* p3  &
 &              +fclinve(16,itb,iz,iy,ix,2) * p4  &
 &              +fclinve(17,itb,iz,iy,ix,2) * p5  &
 &              +fclinve(18,itb,iz,iy,ix,2) * p6
      a5 =dconjg(fclinve( 5,itb,iz,iy,ix,2))* p1  &
 &       +dconjg(fclinve(10,itb,iz,iy,ix,2))* p2  &
 &       +dconjg(fclinve(14,itb,iz,iy,ix,2))* p3  &
 &       +dconjg(fclinve(17,itb,iz,iy,ix,2))* p4  &
 &              +fclinve(19,itb,iz,iy,ix,2) * p5  &
 &              +fclinve(20,itb,iz,iy,ix,2) * p6
      a6 =dconjg(fclinve( 6,itb,iz,iy,ix,2))* p1  &
 &       +dconjg(fclinve(11,itb,iz,iy,ix,2))* p2  &
 &       +dconjg(fclinve(15,itb,iz,iy,ix,2))* p3  &
 &       +dconjg(fclinve(18,itb,iz,iy,ix,2))* p4  &
 &       +dconjg(fclinve(20,itb,iz,iy,ix,2))* p5  &
 &              +fclinve(21,itb,iz,iy,ix,2) * p6

      b1 =       fclinve( 1,itb,iz,iy,ix,1) * m1  &
 &              +fclinve( 2,itb,iz,iy,ix,1) * m2  &
 &              +fclinve( 3,itb,iz,iy,ix,1) * m3  &
 &              +fclinve( 4,itb,iz,iy,ix,1) * m4  &
 &              +fclinve( 5,itb,iz,iy,ix,1) * m5  &
 &              +fclinve( 6,itb,iz,iy,ix,1) * m6
      b2 =dconjg(fclinve( 2,itb,iz,iy,ix,1))* m1  &
 &              +fclinve( 7,itb,iz,iy,ix,1) * m2  &
 &              +fclinve( 8,itb,iz,iy,ix,1) * m3  &
 &              +fclinve( 9,itb,iz,iy,ix,1) * m4  &
 &              +fclinve(10,itb,iz,iy,ix,1) * m5  &
 &              +fclinve(11,itb,iz,iy,ix,1) * m6
      b3 =dconjg(fclinve( 3,itb,iz,iy,ix,1))* m1  &
 &       +dconjg(fclinve( 8,itb,iz,iy,ix,1))* m2  &
 &              +fclinve(12,itb,iz,iy,ix,1) * m3  &
 &              +fclinve(13,itb,iz,iy,ix,1) * m4  &
 &              +fclinve(14,itb,iz,iy,ix,1) * m5  &
 &              +fclinve(15,itb,iz,iy,ix,1) * m6
      b4 =dconjg(fclinve( 4,itb,iz,iy,ix,1))* m1  &
 &       +dconjg(fclinve( 9,itb,iz,iy,ix,1))* m2  &
 &       +dconjg(fclinve(13,itb,iz,iy,ix,1))* m3  &
 &              +fclinve(16,itb,iz,iy,ix,1) * m4  &
 &              +fclinve(17,itb,iz,iy,ix,1) * m5  &
 &              +fclinve(18,itb,iz,iy,ix,1) * m6
      b5 =dconjg(fclinve( 5,itb,iz,iy,ix,1))* m1  &
 &       +dconjg(fclinve(10,itb,iz,iy,ix,1))* m2  &
 &       +dconjg(fclinve(14,itb,iz,iy,ix,1))* m3  &
 &       +dconjg(fclinve(17,itb,iz,iy,ix,1))* m4  &
 &              +fclinve(19,itb,iz,iy,ix,1) * m5  &
 &              +fclinve(20,itb,iz,iy,ix,1) * m6
      b6 =dconjg(fclinve( 6,itb,iz,iy,ix,1))* m1  &
 &       +dconjg(fclinve(11,itb,iz,iy,ix,1))* m2  &
 &       +dconjg(fclinve(15,itb,iz,iy,ix,1))* m3  &
 &       +dconjg(fclinve(18,itb,iz,iy,ix,1))* m4  &
 &       +dconjg(fclinve(20,itb,iz,iy,ix,1))* m5  &
 &              +fclinve(21,itb,iz,iy,ix,1) * m6

      yde(1,1,itb,iz,iy,ix) = a1 + b1
      yde(2,1,itb,iz,iy,ix) = a2 + b2
      yde(3,1,itb,iz,iy,ix) = a3 + b3
      yde(1,2,itb,iz,iy,ix) = a4 + b4
      yde(2,2,itb,iz,iy,ix) = a5 + b5
      yde(3,2,itb,iz,iy,ix) = a6 + b6
      yde(1,3,itb,iz,iy,ix) = a1 - b1
      yde(2,3,itb,iz,iy,ix) = a2 - b2
      yde(3,3,itb,iz,iy,ix) = a3 - b3
      yde(1,4,itb,iz,iy,ix) = a4 - b4
      yde(2,4,itb,iz,iy,ix) = a5 - b5
      yde(3,4,itb,iz,iy,ix) = a6 - b6

    enddo
  enddo
  enddo
  enddo
!$OMP END DO
!$OMP END PARALLEL

  return
end subroutine
