subroutine expp_u(dt,pe,ue_t_,ieo)
!***********************************************************************
!$Id: expp_u.h90,v 1.1 2009/12/02 10:24:23 ishikawa Exp $
!
! Compute SU(3) group matrix U from su(3) Lie algebra 
! matrix P using matrix exponental formula
!
!    dt : time step
!    pe : even/odd site, su(3) Lie algebra matrix
!    ue : even/odd site gauge link, SU(3) group matrix,
!           ue(n,mu)=exp(I*dt*pe(n,mu))
!   ieo : operation index
!           ieo=0 for even site
!           ieo=1 for  odd site
!
!***********************************************************************
  implicit none
  real(8), intent(in) :: dt
  complex(8), intent(inout) ::  &
 &                          ue_t(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM)
  complex(8), intent(in) :: pe  (COL,COL,0:NTH,NZ,NY,NX,NDIM)
  integer, intent(in) :: ieo
!*****
  complex(8), parameter :: zi=(0.0d0,1.0d0)
  complex(8) :: coef(COL,0:NTH)
  complex(8) ::    W(COL,COL,0:NTH)
  complex(8) ::   W2(COL,COL,0:NTH)
  complex(8) :: expP(COL,COL,0:NTH)
  real(8) :: t2(0:NTH),t3(0:NTH)
  integer :: ix,iy,iz,itb,ieoxyz,mu,ic,jc

  do mu=1,NDIM
  do ix=1,NX
  do iy=1,NY
  do iz=1,NZ
    ieoxyz=mod(ipeo+ix+iy+iz+ieo,2)

    do itb=1-ieoxyz,NTH-ieoxyz
      do jc = 1,COL
      do ic = 1,COL
        W(ic,jc,itb)=dt*pe(ic,jc,itb,iz,iy,ix,mu)
      enddo
      enddo
    enddo

!***************************
! W2 = W^2
!***************************
    do itb=1-ieoxyz,NTH-ieoxyz
      do jc = 1,COL
      do ic = 1,COL
        W2(ic,jc,itb)= W(ic,1,itb)*W(1,jc,itb)  &
 &                    +W(ic,2,itb)*W(2,jc,itb)  &
 &                    +W(ic,3,itb)*W(3,jc,itb)
      enddo
      enddo
    enddo

!***************************
! tr[W^2]/2
!***************************
    do itb=1-ieoxyz,NTH-ieoxyz
      t2(itb) = real(W2(1,1,itb)  &
 &                  +W2(2,2,itb)  &
 &                  +W2(3,3,itb))/2.0d0
    enddo
!***************************
! tr[W^3]/3
!***************************
    do itb=1-ieoxyz,NTH-ieoxyz
      t3(itb) = ( real(W(1,1,itb))**3   &
 &               +real(W(2,2,itb))**3   &
 &               +real(W(3,3,itb))**3 )/3.0d0  &
 &    +( real(W(1,1,itb))*( real(W(1,2,itb))**2+aimag(W(1,2,itb))**2    &
 &                         +real(W(1,3,itb))**2+aimag(W(1,3,itb))**2 )  &
 &      +real(W(2,2,itb))*( real(W(1,2,itb))**2+aimag(W(1,2,itb))**2    &
 &                         +real(W(2,3,itb))**2+aimag(W(2,3,itb))**2 )  &
 &      +real(W(3,3,itb))*( real(W(1,3,itb))**2+aimag(W(1,3,itb))**2    &
 &                         +real(W(2,3,itb))**2+aimag(W(2,3,itb))**2 )  &
 &      +real(W(1,2,itb)*W(2,3,itb)*W(3,1,itb))*2.0d0 )
    enddo

!************************************************
! calc. exp(i W) with 12th order Taylor expansion
!************************************************
    do itb=1-ieoxyz,NTH-ieoxyz
    coef(1,itb)=2.0876756987868099d-9*(-1.32d2*t2(itb)**4+t2(itb)**5     &
 &  +2.4d1*t2(itb)**3*(4.95d2-(0,2.d0)*t3(itb))-3.96d2*t2(itb)*(-5.04d4  &
 &  +(0,4.8d2)*t3(itb)+t3(itb)**2)+6.d0*t2(itb)**2*(-1.1088d5+(0,        &
 &  6.6d2)*t3(itb)+t3(itb)**2)+1.2d1*(-1.99584d7+(0,3.3264d5)*t3(itb)    &
 &  +9.9d2*t3(itb)**2-(0,1.d0)*t3(itb)**3))
    coef(2,itb)=(0,-2.0876756987868099d-9)*(1.2d1*t2(itb)**5+5.28d2*     &
 &  t2(itb)**3*(1.8d2-(0,1.d0)*t3(itb))+(0,5.d0)*t2(itb)**4*((0,2.64d2)  &
 &  +t3(itb))+7.2d1*t2(itb)**2*(-5.544d4+(0,4.95d2)*t3(itb)+t3(itb)**2)  &
 &  +t2(itb)*(7.98336d7-(0,1.33056d6)*t3(itb)-3.96d3*t3(itb)**2          &
 &  +(0,4.d0)*t3(itb)**3)-(0,1.32d2)*((0,-3.6288d6)-1.512d5*t3(itb)      &
 &  +(0,7.2d2)*t3(itb)**2+t3(itb)**3))
    coef(3,itb)=2.0876756987868099d-9*(4.790016d8-(0,1.2d1)*(6.6528d6    &
 &  -3.3264d5*t2(itb)+7.92d3*t2(itb)**2-1.1d2*t2(itb)**3+t2(itb)**4      &
 &  )*t3(itb)+4.d0*(-1.6632d5+5.94d3*t2(itb)-9.9d1*t2(itb)**2+t2(itb)**3)  &
 &  *t3(itb)**2-(0,1.2d1)*(-1.1d2+3.d0*t2(itb))*t3(itb)**3+t3(itb)**4)
    enddo

!***************************
! calc exp(i W) with Cayley-Hamilton theorem
!***************************
    do itb=1-ieoxyz,NTH-ieoxyz
      expP(1,1,itb)=coef(1,itb)*W2(1,1,itb)  &
 &                 +coef(2,itb)* W(1,1,itb)+coef(3,itb)
      expP(2,1,itb)=coef(1,itb)*W2(2,1,itb)  &
 &                 +coef(2,itb)* W(2,1,itb)
      expP(3,1,itb)=coef(1,itb)*W2(3,1,itb)  &
 &                 +coef(2,itb)* W(3,1,itb)
      expP(1,2,itb)=coef(1,itb)*W2(1,2,itb)  &
 &                 +coef(2,itb)* W(1,2,itb)
      expP(2,2,itb)=coef(1,itb)*W2(2,2,itb)  &
 &                 +coef(2,itb)* W(2,2,itb)+coef(3,itb)
      expP(3,2,itb)=coef(1,itb)*W2(3,2,itb)  &
 &                 +coef(2,itb)* W(3,2,itb)
      expP(1,3,itb)=coef(1,itb)*W2(1,3,itb)  &
 &                 +coef(2,itb)* W(1,3,itb)
      expP(2,3,itb)=coef(1,itb)*W2(2,3,itb)  &
 &                 +coef(2,itb)* W(2,3,itb)
      expP(3,3,itb)=coef(1,itb)*W2(3,3,itb)  &
 &                 +coef(2,itb)* W(3,3,itb)+coef(3,itb)
    enddo

!***************************
! mul  exp(i W) on u
!***************************
    do itb=1-ieoxyz,NTH-ieoxyz
      do jc = 1,COL
      do ic = 1,COL
        W(ic,jc,itb)=  &
 &       expP(ic,1,itb)*ue_t(1,jc,itb,iz,iy,ix,mu)  &
 &      +expP(ic,2,itb)*ue_t(2,jc,itb,iz,iy,ix,mu)  &
 &      +expP(ic,3,itb)*ue_t(3,jc,itb,iz,iy,ix,mu)
      enddo
      enddo
    enddo

    do itb=1-ieoxyz,NTH-ieoxyz
      do jc = 1,COL
      do ic = 1,COL
        ue_t(ic,jc,itb,iz,iy,ix,mu)=W(ic,jc,itb)
      enddo
      enddo
    enddo

  enddo
  enddo
  enddo ! end of do ix,iy,iz

  call copy_u(ue_t_,mu,ieo)

  enddo ! end of do mu

  return
end subroutine
