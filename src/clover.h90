  subroutine clover(ucle,uclo,ue,uo)
!***********************************************************************
!$Id: clover.h90,v 1.1 2009/12/02 10:24:23 ishikawa Exp $
! Calculate clover term for SW(clover) fermion in chiral representation
!
!  ucle = F_{mu,nu}
!
!    ucle : even site clover term
!    uclo : odd site clover term
!      ue : even site gauge link
!      uo : odd site gauge link
!
!***********************************************************************
  implicit none
  complex(8), intent(in) :: ue(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM)
  complex(8), intent(in) :: uo(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM)
  complex(8), intent(out)::  &
 &                       ucle(COL,COL,NTH,NZ,NY,NX,NDIM*(NDIM-1)/2),  &
 &                       uclo(COL,COL,NTH,NZ,NY,NX,NDIM*(NDIM-1)/2)
!*********** 
  complex(8), allocatable :: wce(:,:,:,:,:,:,:)
  complex(8), allocatable :: wco(:,:,:,:,:,:,:)
  complex(8), allocatable :: ve(:,:,:,:,:,:)
  complex(8), allocatable :: vo(:,:,:,:,:,:)
  integer :: mu,nu,ipl,ic,jc
  integer :: ix,iy,iz,itb,ieoxyz
  integer :: ixlow,iylow,izlow,itlow
  integer :: ixhig,iyhig,izhig,ithig
  integer :: itb0
  integer :: ix1,iy1,iz1,itb1
  integer :: ix2,iy2,iz2,itb2
  integer :: ix3,iy3,iz3,itb3
  integer :: ix4,iy4,iz4,itb4
  integer :: ix5,iy5,iz5,itb5
  integer :: ix6,iy6,iz6,itb6
  complex(8) :: zid8
  complex(8), parameter :: zi=(0.0d0,1.0d0)

  allocate(wce(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM))
  allocate(wco(COL,COL,0:NTH,0:NZ1,0:NY1,0:NX1,NDIM))
  allocate(ve(COL,COL,NTH,NZ,NY,NX))
  allocate(vo(COL,COL,NTH,NZ,NY,NX))


  zid8=-zi/8.0d0
!***********************************************************************
!  super loop over hyperplanes (extends to end of subroutine)
!  mu < nu
!***********************************************************************
  do mu=1,NDIM-1
  do nu=mu+1,NDIM

    ipl = imunu(mu,nu)

!***********************************************************************
! upper plaquette
!
!     4---<---3             4                  4---<---3
!     |	      |             |	                       |
! nu  v       ^             v  w(1)              w(2)  ^ 
!     |	      |             |                          |
!     n	-->---2             n -->---2          n       2
!	  mu
!
!***********************************************************************
    ixlow = 1-isx(mu)-isx(nu)
    iylow = 1-isy(mu)-isy(nu)
    izlow = 1-isz(mu)-isz(nu)
    itlow = 1-ist(mu)-ist(nu)
!$OMP PARALLEL DO PRIVATE(ix,iy,iz,ieoxyz,itb,ic,jc,ix2,iy2,iz2,itb2,ix4,iy4,iz4,itb4) COLLAPSE(2)
    do ix=ixlow,NX
    do iy=iylow,NY
    do iz=izlow,NZ
      ieoxyz=mod(ipeo+ix+iy+iz,2)
      ix2=ix+isx(mu)
      iy2=iy+isy(mu)
      iz2=iz+isz(mu)
      ix4=ix+isx(nu)
      iy4=iy+isy(nu)
      iz4=iz+isz(nu)

      do itb=(1-ieoxyz)*itlow,NTH-ieoxyz
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wce(ic,jc,itb,iz,iy,ix,1)=  &
 &              conjg(ue( 1,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    ue( 1,jc,itb ,iz ,iy ,ix ,mu)    &
 &             +conjg(ue( 2,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    ue( 2,jc,itb ,iz ,iy ,ix ,mu)    &
 &             +conjg(ue( 3,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    ue( 3,jc,itb ,iz ,iy ,ix ,mu)
        enddo
        enddo
      enddo
      do itb=(ieoxyz)*itlow,NTH+ieoxyz-1
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wco(ic,jc,itb,iz,iy,ix,1)=  &
 &              conjg(uo( 1,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    uo( 1,jc,itb ,iz ,iy ,ix ,mu)    &
 &             +conjg(uo( 2,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    uo( 2,jc,itb ,iz ,iy ,ix ,mu)    &
 &             +conjg(uo( 3,ic,itb ,iz ,iy ,ix ,nu))*  &
 &                    uo( 3,jc,itb ,iz ,iy ,ix ,mu)
        enddo
        enddo
      enddo
      do itb=(1-ieoxyz)*itlow,NTH-ieoxyz
        itb2=itb+ieoxyz*ist(mu)
        itb4=itb+ieoxyz*ist(nu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wce(ic,jc,itb,iz,iy,ix,2) =   &
 &                    uo(ic, 1,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(uo(jc, 1,itb4,iz4,iy4,ix4,mu))   &
 &             +      uo(ic, 2,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(uo(jc, 2,itb4,iz4,iy4,ix4,mu))   &
 &             +      uo(ic, 3,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(uo(jc, 3,itb4,iz4,iy4,ix4,mu))
        enddo
        enddo
      enddo
      do itb=(ieoxyz)*itlow,NTH+ieoxyz-1
        itb2=itb+(1-ieoxyz)*ist(mu)
        itb4=itb+(1-ieoxyz)*ist(nu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wco(ic,jc,itb,iz,iy,ix,2) =   &
 &                    ue(ic, 1,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(ue(jc, 1,itb4,iz4,iy4,ix4,mu))   &
 &             +      ue(ic, 2,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(ue(jc, 2,itb4,iz4,iy4,ix4,mu))   &
 &             +      ue(ic, 3,itb2,iz2,iy2,ix2,nu) *  &
 &              conjg(ue(jc, 3,itb4,iz4,iy4,ix4,mu))
        enddo
        enddo
      enddo
    enddo
    enddo
    enddo ! end of do ix,iy,iz

!***********************************************************************
! lower plaquette
!
!     n --<---2             n --<---2        n       2 
!     |	      |             |                        |	      
! nu  v       ^             v  w(3)            w(4)  ^ 
!     |	      |             |                 	     |   
!     5--->---6  	    5                5--->---6
!	  mu
!
!***********************************************************************
    ixlow = 1-isx(mu)
    iylow = 1-isy(mu)
    izlow = 1-isz(mu)
    itlow = 1-ist(mu)
    ixhig = NX+isx(nu)
    iyhig = NY+isy(nu)
    izhig = NZ+isz(nu)
    ithig = 1-ist(nu)
!$OMP PARALLEL DO PRIVATE(ix,iy,iz,ieoxyz,itb,ic,jc,ix5,iy5,iz5,itb5,ix6,iy6,iz6,itb6) COLLAPSE(2)
    do ix=ixlow,ixhig
    do iy=iylow,iyhig
    do iz=izlow,izhig
      ieoxyz=mod(ipeo+ix+iy+iz,2)
      ix5=ix-isx(nu)
      iy5=iy-isy(nu)
      iz5=iz-isz(nu)
      ix6=ix5+isx(mu)
      iy6=iy5+isy(mu)
      iz6=iz5+isz(mu)

      do itb=(1-ieoxyz)*itlow,NTH-(ieoxyz)*ithig
        itb5=itb-(1-ieoxyz)*ist(nu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wce(ic,jc,itb,iz,iy,ix,3) =   &
 &              conjg(ue( 1,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(uo(jc, 1,itb5,iz5,iy5,ix5,nu))   &
 &             +conjg(ue( 2,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(uo(jc, 2,itb5,iz5,iy5,ix5,nu))   &
 &             +conjg(ue( 3,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(uo(jc, 3,itb5,iz5,iy5,ix5,nu))
        enddo
        enddo
      enddo
      do itb=(ieoxyz)*itlow,NTH+(ieoxyz-1)*ithig
        itb5=itb-  (ieoxyz)*ist(nu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wco(ic,jc,itb,iz,iy,ix,3) =   &
 &              conjg(uo( 1,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(ue(jc, 1,itb5,iz5,iy5,ix5,nu))   &
 &             +conjg(uo( 2,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(ue(jc, 2,itb5,iz5,iy5,ix5,nu))   &
 &             +conjg(uo( 3,ic,itb ,iz ,iy ,ix ,mu))*  &
 &              conjg(ue(jc, 3,itb5,iz5,iy5,ix5,nu))
        enddo
        enddo
      enddo
      do itb=(1-ieoxyz)*itlow,NTH-(ieoxyz)*ithig
        itb5=itb-(1-ieoxyz)*ist(nu)
        itb6=itb5  +ieoxyz *ist(mu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wce(ic,jc,itb,iz,iy,ix,4) =  &
 &                    uo(ic, 1,itb5,iz5,iy5,ix5,mu)*  &
 &                    ue( 1,jc,itb6,iz6,iy6,ix6,nu)   &
 &             +      uo(ic, 2,itb5,iz5,iy5,ix5,mu)*  &
 &                    ue( 2,jc,itb6,iz6,iy6,ix6,nu)   &
 &             +      uo(ic, 3,itb5,iz5,iy5,ix5,mu)*  &
 &                    ue( 3,jc,itb6,iz6,iy6,ix6,nu)
        enddo
        enddo
      enddo
      do itb=(ieoxyz)*itlow,NTH+(ieoxyz-1)*ithig
        itb5=itb    -ieoxyz *ist(nu)
        itb6=itb5+(1-ieoxyz)*ist(mu)
        !  198 flop
        do jc = 1,COL
        do ic = 1,COL
          wco(ic,jc,itb,iz,iy,ix,4) =  &
 &                    ue(ic, 1,itb5,iz5,iy5,ix5,mu)*  &
 &                    uo( 1,jc,itb6,iz6,iy6,ix6,nu)   &
 &             +      ue(ic, 2,itb5,iz5,iy5,ix5,mu)*  &
 &                    uo( 2,jc,itb6,iz6,iy6,ix6,nu)   &
 &             +      ue(ic, 3,itb5,iz5,iy5,ix5,mu)*  &
 &                    uo( 3,jc,itb6,iz6,iy6,ix6,nu)
        enddo
        enddo
      enddo
    enddo
    enddo
    enddo ! end of do ix,iy,iz

!***********************************************************************
! form clover leaves and calculate F_{mu,nu}
!
!     ---<---  1---<---
!    | w(2,2)| |w(1,3) |      nu
!    v       ^ v       ^      ^
!    |w(2,1) | | w(1,4)|      |
!    2--->---   --->---       --->mu
!             n
!    2---<---   ---<---
!    |w(2,3) | | w(2,2)|
!    V       ^ v       ^
!    | w(2,4)| |w(3,1) |
!     --->---  3--->---
!
!***********************************************************************
!$OMP PARALLEL PRIVATE(ix,iy,iz,ieoxyz,itb,ic,jc,itb0,ix1,iy1,iz1,itb1,ix2,iy2,iz2,itb2,ix3,iy3,iz3,itb3)
!$OMP DO COLLAPSE(2)
    do ix=1,NX
    do iy=1,NY
    do iz=1,NZ
      ieoxyz=mod(ipeo+ix+iy+iz,2)
      ix1=ix+isx(nu)
      iy1=iy+isy(nu)
      iz1=iz+isz(nu)
      ix2=ix-isx(mu)
      iy2=iy-isy(mu)
      iz2=iz-isz(mu)
      ix3=ix-isx(nu)
      iy3=iy-isy(nu)
      iz3=iz-isz(nu)

      do itb=1-ieoxyz,NTH-ieoxyz
        itb0=itb+ieoxyz
        itb1=itb   +ieoxyz *ist(nu)               
        itb2=itb-(1-ieoxyz)*ist(mu)               
        itb3=itb-(1-ieoxyz)*ist(nu)               
        !  846 flop
        do jc=1,COL
        do ic=1,COL
          ve(ic,jc,itb0,iz,iy,ix) =   &
 &         wco(ic,1,itb1,iz1,iy1,ix1,4)*wco(1,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wco(ic,2,itb1,iz1,iy1,ix1,4)*wco(2,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wco(ic,3,itb1,iz1,iy1,ix1,4)*wco(3,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wco(ic,1,itb2,iz2,iy2,ix2,2)*wco(1,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wco(ic,2,itb2,iz2,iy2,ix2,2)*wco(2,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wco(ic,3,itb2,iz2,iy2,ix2,2)*wco(3,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wco(ic,1,itb2,iz2,iy2,ix2,3)*wco(1,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wco(ic,2,itb2,iz2,iy2,ix2,3)*wco(2,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wco(ic,3,itb2,iz2,iy2,ix2,3)*wco(3,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wco(ic,1,itb3,iz3,iy3,ix3,1)*wco(1,jc,itb3,iz3,iy3,ix3,2)  &
 &        +wco(ic,2,itb3,iz3,iy3,ix3,1)*wco(2,jc,itb3,iz3,iy3,ix3,2)  &
 &        +wco(ic,3,itb3,iz3,iy3,ix3,1)*wco(3,jc,itb3,iz3,iy3,ix3,2)
        enddo
        enddo
      enddo
      do itb=ieoxyz,NTH+ieoxyz-1
        itb0=itb+ 1-ieoxyz
        itb1=itb+(1-ieoxyz)*ist(nu)               
        itb2=itb   -ieoxyz *ist(mu)               
        itb3=itb   -ieoxyz *ist(nu)               
        !  846 flop
        do jc=1,COL
        do ic=1,COL
          vo(ic,jc,itb0,iz,iy,ix) =   &
 &         wce(ic,1,itb1,iz1,iy1,ix1,4)*wce(1,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wce(ic,2,itb1,iz1,iy1,ix1,4)*wce(2,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wce(ic,3,itb1,iz1,iy1,ix1,4)*wce(3,jc,itb1,iz1,iy1,ix1,3)  &
 &        +wce(ic,1,itb2,iz2,iy2,ix2,2)*wce(1,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wce(ic,2,itb2,iz2,iy2,ix2,2)*wce(2,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wce(ic,3,itb2,iz2,iy2,ix2,2)*wce(3,jc,itb2,iz2,iy2,ix2,1)  &
 &        +wce(ic,1,itb2,iz2,iy2,ix2,3)*wce(1,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wce(ic,2,itb2,iz2,iy2,ix2,3)*wce(2,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wce(ic,3,itb2,iz2,iy2,ix2,3)*wce(3,jc,itb2,iz2,iy2,ix2,4)  &
 &        +wce(ic,1,itb3,iz3,iy3,ix3,1)*wce(1,jc,itb3,iz3,iy3,ix3,2)  &
 &        +wce(ic,2,itb3,iz3,iy3,ix3,1)*wce(2,jc,itb3,iz3,iy3,ix3,2)  &
 &        +wce(ic,3,itb3,iz3,iy3,ix3,1)*wce(3,jc,itb3,iz3,iy3,ix3,2)
        enddo
        enddo
      enddo

!***********************************************************************
!  total clover (with counter clockwise plaquettes)
!  ucl(ipl) = -zi ( v - v^{+} ) / 8
!***********************************************************************
      do itb=1,NTH
        !  72*2 flop
        do jc=1,COL
        do ic=1,COL
          ucle(ic,jc,itb,iz,iy,ix,ipl)=  &
 &                    (ve(ic,jc,itb,iz,iy,ix)-conjg(ve(jc,ic,itb,iz,iy,ix)))*zid8
          uclo(ic,jc,itb,iz,iy,ix,ipl)=  &
 &                    (vo(ic,jc,itb,iz,iy,ix)-conjg(vo(jc,ic,itb,iz,iy,ix)))*zid8
        enddo
        enddo
      enddo

    enddo
    enddo
    enddo ! end of do ix,iy,iz
!$OMP END DO
!$OMP END PARALLEL

  enddo ! end of do nu
  enddo ! end of do mu

  deallocate(wce,wco,ve,vo)

  return
end subroutine
